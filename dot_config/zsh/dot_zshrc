# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# =============================================================
#                       homebrew completion
# =============================================================
BREW_CMP_DIR="$(brew --prefix)/share/zsh/site-functions"
FPATH="${BREW_CMP_DIR}:${FPATH}"

# =============================================================
#                       MACOS tweak
# =============================================================
if [[ "$(uname)" == "Darwin" ]]; then
    export ZSH_IN_MACOS=true
    export HOMEBREW_CASK_OPTS="--appdir=/Applications --fontdir=/Library/Fonts"
    if [[ "$(stat -f '%Su' ${BREW_CMP_DIR})" != "${USER}" ]]; then
        ZSH_COMPINIT_OPTS="-u"
    fi
fi
unset BREW_CMP_DIR

# =============================================================
#                   Zsh State Management
# =============================================================
# Interactive shell settings
export SHELL_SESSIONS_DISABLE=1
mkdir -p $HOME/.local/state/less
mkdir -p $HOME/.local/state/zsh

export LESSHISTFILE="$HOME/.local/state/less/history"
HISTFILE="$HOME/.local/state/zsh/history"
SAVEHIST=10000
HISTSIZE=10000

# =============================================================
#                      Zinit Setup
# =============================================================
# Configure zinit paths
declare -A ZINIT
ZINIT[ZCOMPDUMP_PATH]="$ZDOTDIR/.zcompdump"

# Install zinit if not present
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[[ ! -d $ZINIT_HOME ]] && mkdir -p "$(dirname $ZINIT_HOME)"
[[ ! -d $ZINIT_HOME/.git ]] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

# Load OMZ Git library (for theme compatibility)
zinit snippet OMZL::git.zsh
zinit snippet OMZL::key-bindings.zsh

# Completions - use blockf to prevent fpath pollution
zinit ice blockf; zinit light zsh-users/zsh-completions

# fzf-tab setup
zstyle ':fzf-tab:*' use-fzf-default-opts yes
zinit light Aloxaf/fzf-tab

# These need to be after fzf-tab
zinit light zsh-users/zsh-autosuggestions
zinit light zsh-users/zsh-syntax-highlighting

# Theme
zinit ice depth=1; zinit light romkatv/powerlevel10k

# Initialize completions
autoload -Uz compinit
compinit $ZSH_COMPINIT_OPTS
zinit cdreplay -q  # replay compdefs

unset ZSH_COMPINIT_OPTS

# =============================================================
#                       FZF Config (before color theme)
# =============================================================

# global ignnore file is $HOME/.config/fd/ignore
export FZF_DEFAULT_COMMAND="fd --type file --follow --hidden"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_C_COMMAND="fd --type dir --follow --hidden"

export MY_FZF_OPTS_NO_COLOR=" --height 60% --layout reverse --info=inline"
# export FZF_CTRL_T_OPTS='--preview "bat --color=always {}"'
export FZF_ALT_C_OPTS='--preview "tree -C {}"'

# FZF keybindings: must be after fzf-tab
source <(fzf --zsh) 

# rg config
export RIPGREP_CONFIG_PATH="$HOME/.config/rg/ripgreprc"

# =============================================================
#                       Color theme
# =============================================================

# apply-theme quick-cmd
source $HOME/.config/colortheme/apply-theme.sh && __apply_theme_for_rc;
alias apply-theme="__apply_theme_all"
# macos LS_COLORS
if [[ $ZSH_IN_MACOS ]] then
    command gls &> /dev/null && alias ls="gls --color"
fi

# =============================================================
#                      User Aliases
# =============================================================

# Set EDITOR
export EDITOR=nvim
export SYSTEMD_EDITOR=nvim
export SUDO_EDITOR=nvim

# MANPAGER
export MANPAGER="nvim +Man!"

# Source custom aliases
source "$ZDOTDIR/alias.zsh"

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f $ZDOTDIR/p10k.zsh ]] || source $ZDOTDIR/p10k.zsh
